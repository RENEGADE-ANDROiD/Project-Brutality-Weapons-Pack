ACTOR HellPistolerFirer : HellBullet
{
Damage 35
Speed 100
DamageType Fire
}

ACTOR HellPistolerFirerSingle : HellBullet
{
+Ripper
Damage 50
Speed 350
DamageType Fire
}

ACTOR HellPistolerFirerCharge : HellBullet
{
+Ripper
Damage 250
DamageType Fire
Speed 350
Scale 1.5
Height 2.0
}

ACTOR HellPistoler : PBWP_Weapon
{
	Weapon.SelectionOrder 2545
    Weapon.AmmoType1 "PB_Cell"
    Weapon.AmmoGive1 300
	Weapon.AmmoUse1 0
	Weapon.AmmoType2 "HellPistolerAmmo"
    Weapon.AmmoGive2 300
	Weapon.AmmoUse2 0
	Weapon.SlotNumber 2
	Weapon.SlotPriority 0.5
	+WEAPON.NOAUTOAIM
    +WEAPON.NOALERT
    +WEAPON.NOAUTOFIRE
    +WEAPON.AMMO_OPTIONAL
	+WEAPON.NO_AUTO_SWITCH
    Inventory.PickupSound "CHGNPKUP"
	Inventory.Pickupmessage  "You Found an Annihilation Assasin (Slot-3)"
	Obituary "%o was decapitated by %k's Assasin."
    AttackSound "None"
    Scale 0.8
	Tag "UAC-UM-03âˆ… HP Plasma Blaster - Annihilation Assassin"
	States
	{
	
	Steady:
		TNT1 A 1
		Goto Ready
	
	Spawn:
		ARMZ A -1
		Stop
		
		Ready:
			TNT1 A 0 A_JumpIfInventory("GoFatality", 1, "Steady")
			AMGR A 0
			"####" A 0 A_PlaySound("weapons/smg_magfly1")
			"####" FEDCBA 1
			Ready3:
			TNT1 A 0 {
					A_TakeInventory("PB_LockScreenTilt",1);
					PB_HandleCrosshair(39);
					}
			ReadyLoop:
			TNT1 A 0 A_JumpIfInventory("GoFatality", 1, "Steady")
			AMGL A 0
			"####" A 1 A_DoPBWeaponAction
			Loop
		
		Deselect:
			TNT1 A 0 A_JumpIfInventory ("GrabbedBarrel", 1, "PlaceBarrel")
			TNT1 A 0 A_JumpIfInventory ("GrabbedFlameBarrel", 1, "PlaceFlameBarrel")
			TNT1 A 0 A_JumpIfInventory ("GrabbedIceBarrel", 1, "PlaceIceBarrel")
			TNT1 A 0 {
				A_WeaponOffset(0,32);
				A_SetRoll(0);
				PB_HandleCrosshair(39);
				A_TakeInventory("PB_LockScreenTilt",1);
			}
			TNT1 A 0 A_TakeInventory("Spin",1)
			TNT1 A 0 A_StopSound(1)
			AMGR A 0
			"####" ABCDEF 1
			TNT1 A 0 A_Lower
			Wait
			
		Select:
			TNT1 A 0 {
				A_WeaponOffset(0,32);
				A_SetRoll(0);
				PB_HandleCrosshair(39);
				A_TakeInventory("PB_LockScreenTilt",1);
			}
			TNT1 A 0 A_TakeInventory("HasBarrel",1)
			TNT1 A 0 A_TakeInventory("HasIceBarrel",1)
			TNT1 A 0 A_TakeInventory("HasFlameBarrel",1)
			TNT1 A 0 A_TakeInventory("GrabbedBarrel",1)
			TNT1 A 0 A_TakeInventory("GrabbedIceBarrel",1)
			TNT1 A 0 A_TakeInventory("GrabbedFlameBarrel",1)
			Goto SelectFirstPersonLegs
			SelectContinue:
			TNT1 A 0 
			TNT1 A 0 A_Raise
			wait
			
		Noammo:
		TNT1 A 0
		Goto ReadyLoop
			
		Fire:
		TNT1 A 0 A_JumpIfInventory ("GrabbedBarrel", 1, "ThrowBarrel")
		TNT1 A 0 A_JumpIfInventory ("GrabbedFlameBarrel", 1, "ThrowFlameBarrel")
		TNT1 A 0 A_JumpIfInventory ("GrabbedIceBarrel", 1, "ThrowIceBarrel")
		TNT1 A 0 {
// 			A_GiveInventory("PB_LockScreenTilt",1);
			A_WeaponOffset(0,32);
			A_SetRoll(0);
			A_SetCrosshair(39);
			if(CountInv("NoFatality") == 0 && GetCVAR ("ttwcfbex") == 1) {
				return PB_Execute();
			}
			return state("");
		}
		TNT1 A 0 A_JumpIfInventory("PistolerMod", 1, "SemiFire")
		TNT1 A 0 A_JumpIfInventory ("HellPistolerAmmo", 1, 2)
		Goto Reload
		AMGF A 1
		AMGF B 1 A_PlaySound("BEP",3)
		AMGF C 1
		Hold:
		TNT1 A 0 A_JumpIfInventory ("HellPistolerAmmo", 1, 2)
		Goto Reload
		TNT1 A 0
		TNT1 A 0 A_PlaySound("HRFire", CHAN_WEAPON)
		AMGF D 1 A_FireCustomMissile("HellPistolerFirerSingle")
		AMGF E 1 A_TakeInventory("HellPistolerAmmo",1)
		AMGF F 1
		TNT1 A 0 PB_ReFire("Hold")
		Goto ReadyLoop
		
	SemiFire:
		TNT1 A 0 A_JumpIfInventory ("HellPistolerAmmo", 1, 2)
		Goto Reload
		AMGF A 1
		AMGF B 1 A_PlaySound("BEP",3)
		AMGF C 1
		TNT1 A 0 A_JumpIfInventory ("HellPistolerAmmo", 1, 2)
		Goto Reload
		TNT1 A 0
		TNT1 A 0 A_PlaySound("HRFire", CHAN_WEAPON)
		AMGF D 1 A_FireCustomMissile("HellPistolerFirer")
		AMGF E 1 A_TakeInventory("HellPistolerAmmo",1)
		AMGF F 1
		Goto ReadyLoop
		
	QuickMelee:
        	"####" A 0 {
				A_StopSound(CHAN_WEAPON);
				A_StopSound(CHAN_VOICE);
				A_StopSound(CHAN_6);
				A_StopSound(CHAN_7);
			}
			TNT1 A 0 A_JumpIfInventory("CantDoAction",1,"FailOverlay")
			TNT1 A 0 A_JumpIfHealthLower(0, "FailOverlay")
			TNT1 A 0 A_JumpIf(!CustomMelee, "Super::GoMeleeInstead")
			TNT1 A 0 {
				A_ClearOverlays(-10,65);
				A_Gunflash("Null");
			}
		"####" AAA 0 PB_Execute()
	GoMeleeInstead:
			AX21 ABCDEF 0
			AX22 ABCDEF 0
			AX23 ABCDEF 0
			TNT1 A 0 {
				A_PlaySound("AXSWING");
				A_SetRoll(0);
				A_SetInventory("AxeSequence1", 0);
				A_SetInventory("AxeSequence2", 1);
			}
			AX20 ABC 1 {
				if (CountInv("PowerGreenBloodOnVisor") >= 1 ) { A_SetWeaponSprite("AX23"); }
				if (CountInv("PowerBlueBloodOnVisor") >= 1 ) { A_SetWeaponSprite("AX22"); }
				if (CountInv("PowerBloodOnVisor") >= 1 ) { A_SetWeaponSprite("AX21"); }
				A_Setroll(roll+1.0, SPF_INTERPOLATE);
				A_SetAngle(angle-0.5, SPF_INTERPOLATE);
			}
			TNT1 A 0 {
				if (CountInv("PB_PowerStrength") >= 1 ) {
					A_Saw("", "", 30, "AxePuffs", 0, 120, 0,16);
					A_FireCustomMissile("AxeAttack", 0, 0, 0, 0);
					A_FireCustomMissile("AxeAttack", 0, 0, 0, 0);
				}
				else {
					A_Saw("", "", 15, "AxePuffs", 0, 120, 0,16);
					A_FireCustomMissile("AxeAttack", 0, 0, 0, 0);
				}
			
			}
			AX20 DEF 1 {
				if (CountInv("PowerGreenBloodOnVisor") >= 1 ) { A_SetWeaponSprite("AX23"); }
				if (CountInv("PowerBlueBloodOnVisor") >= 1 ) { A_SetWeaponSprite("AX22"); }
				if (CountInv("PowerBloodOnVisor") >= 1 ) { A_SetWeaponSprite("AX21"); }
				A_Setroll(roll+1.0, SPF_INTERPOLATE);
				A_SetAngle(angle-0.5, SPF_INTERPOLATE);
			}
			TNT1 AAA 1 {
				A_Setroll(roll-1.0, SPF_INTERPOLATE);
				A_SetAngle(angle+0.5, SPF_INTERPOLATE);
			}
			TNT1 AAA 1 {
				A_Setroll(roll-1.0, SPF_INTERPOLATE);
				A_SetAngle(angle+0.5, SPF_INTERPOLATE);
				return A_DoPBWeaponAction;
			}
		TNT1 A 7 {
			 if(JustPressed(BT_USER2)) {return PB_Execute();}
			 return state("");
		     }
		TNT1 A 0 {
			 PB_SetUsingMelee(false);
		     }
		TNT1 A 0 
		Goto Ready


		AltFire:
		TNT1 A 0 A_JumpIfInventory ("GrabbedBarrel", 1, "PlaceBarrel")
		TNT1 A 0 A_JumpIfInventory ("GrabbedFlameBarrel", 1, "PlaceFlameBarrel")
		TNT1 A 0 A_JumpIfInventory ("GrabbedIceBarrel", 1, "PlaceIceBarrel")
		TNT1 A 0 {
			A_WeaponOffset(0,32);
			A_SetRoll(0);
			PB_HandleCrosshair(39);
			A_TakeInventory("PB_LockScreenTilt",1);
			A_ClearOverlays(10,11);
		}
		TNT1 A 0 A_JumpIfInventory("PistolerMod", 1, "ChargeFire")
		TNT1 A 0 A_JumpIfInventory ("HellPistolerAmmo", 3, 2)
		Goto Reload
		AMGF A 1 A_PlaySound("BEP",7)
		AMGF B 1 A_PlaySound("BEP",8)
		AMGF C 1 A_PlaySound("BEP",9)
		Hold2:
		TNT1 A 0 A_JumpIfInventory ("HellPistolerAmmo", 3, 2)
		Goto Reload
		AMGF D 0 A_PlaySound("HRFire")
		AMGF D 1 A_FireCustomMissile("HellPistolerFirer")
		AMGF E 1 A_TakeInventory("HellPistolerAmmo",1)
		AMGF F 1
		AMGF D 0 A_PlaySound("HRFire")
		AMGF D 1 A_FireCustomMissile("HellPistolerFirer")
		AMGF E 1 A_TakeInventory("HellPistolerAmmo",1)
		AMGF F 1
		AMGF D 0 A_PlaySound("HRFire")
		AMGF D 1 A_FireCustomMissile("HellPistolerFirer")
		AMGF E 1 A_TakeInventory("HellPistolerAmmo",1)
		AMGF F 1
		AMGL A 10
		TNT1 A 0 PB_ReFire("Hold2")
		Goto ReadyLoop
		
	ChargeFire:
		TNT1 A 0 A_JumpIfInventory ("HellPistolerAmmo", 2, 2)
		Goto Reload
		AMGF A 1 A_PlaySound("BEP",7)
		AMGF B 1 A_PlaySound("BEP",8)
		AMGF C 1 A_PlaySound("BEP",9)
		Holding:
		AMGF C 5
		OkTillHold:
		AMGF C 1 A_FireCustomMissile("RedFlareSpawn", 0, 0, 0, 0)
		TNT1 A 0 PB_ReFire("OkTillHold")
		Ok:
		AMGF D 0 A_PlaySound("HRFire")
		AMGF D 1 A_FireCustomMissile("HellPistolerFirerCharge")
		AMGF E 1 A_TakeInventory("HellPistolerAmmo",2)
		AMGF F 1
		AMGL A 10
		Goto ReadyLoop
		
	Reload:
		TNT1 A 0 A_JumpIfInventory ("GrabbedBarrel", 1, "IdleBarrel")
		TNT1 A 0 A_JumpIfInventory ("GrabbedFlameBarrel", 1, "IdleFlameBarrel")
		TNT1 A 0 A_JumpIfInventory ("GrabbedIceBarrel", 1, "IdleIceBarrel")
		TNT1 A 0 A_Takeinventory("Reloading",1)
		TNT1 A 0 A_JumpIfInventory("HellPistolerAmmo", 16,"Ready3")
		TNT1 A 0 A_JumpIfInventory("PB_Cell",1,3)
		Goto NoAmmo
		TNT1 AAA 0
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_GiveInventory ("Pumping", 1)
		TNT1 A 0 A_Takeinventory("Reloading",1)
		TNT1 AA 0
		TNT1 A 0 A_PlaySound("weapons/smg_magfly1")
		AMGR ABCDEF 1
		TNT1 A 0 A_PlaySound("weapons/smg_magfly2")
		AMGR GHIJ 1
		TNT1 A 0 A_PlaySound("CELLOUT2", 5)
		AMGR KLMNO 1
		AMGR PQRST 1
		TNT1 A 0 A_PlaySound("CELLIN2", 6)
		AMGR UVWXY 1
		TNT1 A 0 A_JumpIfInventory("HellPistolerAmmo",1,"InsertBullets2")
		TNT1 A 0
		Goto InsertBullets
		
		InsertBullets:
		TNT1 AAAA 0
		TNT1 A 0 A_JumpIfInventory("HellPistolerAmmo",15,"EndReload")
		TNT1 A 0 A_JumpIfInventory("PB_Cell",1,3)
		Goto Ready
		TNT1 AAAAAA 0
		TNT1 A 0 A_Giveinventory("HellPistolerAmmo",1)
		TNT1 A 0 A_Takeinventory("PB_Cell",1,TIF_NOTAKEINFINITE)
		Goto InsertBullets
		
		EndReload:
		TNT1 A 0 A_Takeinventory("Reloading",1)
		TNT1 A 0 PB_ReFire
		Goto Ready
		
		InsertBullets2:
		TNT1 AAAA 0
		TNT1 A 0 A_JumpIfInventory("HellPistolerAmmo",16,"EndReload")
		TNT1 A 0 A_JumpIfInventory("PB_Cell",1,3)
		Goto Ready
		TNT1 AAAAAA 0
		TNT1 A 0 A_Giveinventory("HellPistolerAmmo",1)
		TNT1 A 0 A_Takeinventory("PB_Cell",1,TIF_NOTAKEINFINITE)
		Goto InsertBullets2
		
	WeaponSpecial:
		TNT1 A 0 A_JumpIfInventory ("GrabbedBarrel", 1, "IdleBarrel")
		TNT1 A 0 A_JumpIfInventory ("GrabbedFlameBarrel", 1, "IdleFlameBarrel")
		TNT1 A 0 A_JumpIfInventory ("GrabbedIceBarrel", 1, "IdleIceBarrel")
		TNT1 A 0 A_JumpIfInventory("PistolerMod", 1, "Auto")
		TNT1 A 0 A_PlaySound("BEP")
		TNT1 A 0 A_GiveInventory("PistolerMod", 1)
		TNT1 A 0 A_Print("\ca Fire: Semi-Auto \n \cv AltFire: Charge-Fire")
		TNT1 A 0 A_TakeInventory("GoWeaponSpecialAbility",1)
		Goto Ready3
		
	Auto:
		TNT1 A 0 A_TakeInventory("PistolerMod", 1)
		TNT1 A 0 A_PlaySound("BEP")
		TNT1 A 0 A_Print("\cx Fire: Full-Auto \n \ct AltFire: Burst-Fire")
		TNT1 A 0 A_TakeInventory("GoWeaponSpecialAbility",1)
		Goto Ready3
		
	FlashKicking:
		TNT1 A 0 A_JumpIfInventory ("GrabbedBarrel", 1, "FlashBarrelKicking")
		TNT1 A 0 A_JumpIfInventory ("GrabbedFlameBarrel", 1, "FlashBarrelKicking")
		TNT1 A 0 A_JumpIfInventory ("GrabbedIceBarrel", 1, "FlashBarrelKicking")
		TNT1 A 0
		AMGR BCDEFGHIIIIIIIII 1
		Goto Ready3
		
	FlashAirKicking:
		TNT1 A 0 A_JumpIfInventory ("GrabbedBarrel", 1, "FlashBarrelAirKicking")
		TNT1 A 0 A_JumpIfInventory ("GrabbedFlameBarrel", 1, "FlashBarrelAirKicking")
		TNT1 A 0 A_JumpIfInventory ("GrabbedIceBarrel", 1, "FlashBarrelAirKicking")	
		TNT1 A 0
        	AMGR BCDEFGHIIIIIIIIIII 1
		Goto Ready3
		
	FlashSlideKicking:
		TNT1 A 0 A_JumpIfInventory ("GrabbedBarrel", 1, "FlashBarrelSlideKicking")
		TNT1 A 0 A_JumpIfInventory ("GrabbedFlameBarrel", 1, "FlashBarrelSlideKicking")
		TNT1 A 0 A_JumpIfInventory ("GrabbedIceBarrel", 1, "FlashBarrelSlideKicking")
		TNT1 A 0
		AMGR BCDEFGHIIIII 1
	FlashSlideKickingStop:
		TNT1 A 0 A_JumpIfInventory ("GrabbedBarrel", 1, "FlashBarrelSlideKickingStop")
		TNT1 A 0 A_JumpIfInventory ("GrabbedFlameBarrel", 1, "FlashBarrelSlideKickingStop")
		TNT1 A 0 A_JumpIfInventory ("GrabbedIceBarrel", 1, "FlashBarrelSlideKickingStop")
		TNT1 A 0
		AMGR IIIIIHGFEDCB 1
		Goto Ready3
		
	}
}

ACTOR HellPistolerAmmo : Ammo
{
	+IGNORESKILL
	Inventory.MaxAmount 16
	Inventory.Icon "ARMZA0"
}

Actor PistolerMod : Inventory { Inventory.MaxAmount 1 }