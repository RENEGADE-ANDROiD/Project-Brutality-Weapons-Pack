/////TOKENS//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
ACTOR RespectBallista : Inventory { Inventory.MaxAmount 1 }
ACTOR BallistaDemonicMode : Inventory { Inventory.MaxAmount 1 }
ACTOR BallistaUnloaded : Inventory { Inventory.MaxAmount 1 }
ACTOR BallistaUpgraded : Inventory { Inventory.MaxAmount 1 }
ACTOR SwapToDemonicMode : Inventory { Inventory.MaxAmount 1 }

/////WEAPON//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
ACTOR Prosurv_Ballista : PBWP_weapon {
	Game Doom
	Weapon.AmmoUse1 0
	Weapon.AmmoGive1 15
	Weapon.AmmoUse2 0
	Weapon.AmmoGive2 1
	Weapon.AmmoType1 "PB_HighCalMag"
	Weapon.AmmoType2 "BallistaAmmo"
	Weapon.SlotNumber 4
	Weapon.SlotPriority 0.15
	Weapon.SelectionOrder 300
	Inventory.PickupMessage "Ballista (Slot 4)"
    Inventory.PickupSound "weapons/ballista/drawstring"
	Inventory.AltHudIcon "CBOWS0"
	Obituary "%k Speared %o with a Ballista"
    AttackSound "None"
	Weapon.BobStyle InverseSmooth
    +WEAPON.NOALERT
    +WEAPON.NOAUTOAIM
	+WEAPON.NOAUTOFIRE
	+FORCEXYBILLBOARD
	Scale 0.7
	Tag "Ballista"
		States {	
			// CUSTOM MELEE ANIMATION, JUST UNCOMMENT THIS CODE AND ADD YOUR ANIMATION
			/*QuickMelee:
					"####" A 0 {
						A_StopSound(CHAN_WEAPON);
						A_StopSound(CHAN_VOICE);
						A_StopSound(CHAN_6);
						A_StopSound(CHAN_7);
					}
					TNT1 A 0 A_JumpIfInventory("CantDoAction",1,"FailOverlay")
					TNT1 A 0 A_JumpIfHealthLower(0, "FailOverlay")
					TNT1 A 0 A_JumpIf(!CustomMelee, "Super::GoMeleeInstead")
					TNT1 A 0 {
						A_ClearOverlays(-10,65);
						A_Gunflash("Null");
					}
				"####" AAA 0 PB_Execute()
			GoMeleeInstead:
			TNT1 A 0 {
					A_Overlay(PSP_FLASH, "FlashPunching");
					A_TakeInventory("Zoomed",1);
					A_ZoomFactor(1.0);
					A_PlaySound("AXSWING");
					A_TakeInventory("ADSmode",1);
					A_SetRoll(0);
					A_Overlay(-10, "FirstPersonLegsStand");
				}
			INSERT ANIMATION HERE
			TNT1 A 0 PB_CheckBarrelIdle1()
			Goto Ready*/

/////FLASHES//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
			FlashPunching:
				TNT1 A 0 A_JumpIfInventory("BallistaAmmo",1,1)
				Goto FlashPunchingUnloaded
				TNT1 A 0 A_JumpIfInventory("BallistaDemonicMode", 1, "FlashPunchingUpgraded")
				CBOW ABCDEEEEEEDCBA 1
				stop
			FlashPunchingUpgraded:
				CBOW KLMNOOOOOONMLK 1
				stop
			FlashPunchingUnloaded:
				CBOW FGHIJJJJJJIHGF 1
				stop
			FlashKicking:
				TNT1 A 0 A_JumpIfInventory("BallistaAmmo",1,1)
				Goto FlashKickingUnloaded
				TNT1 A 0 A_JumpIfInventory("BallistaDemonicMode", 1, "FlashKickingUpgraded")
				CRBW LMNOPPPPPPONML 1
				Goto Ready3
			FlashKickingUpgraded:
				CRBW VWXYZZZZZZYXWV 1
				Goto Ready3
			FlashKickingUnloaded:
				CRBW QRSTUUUUUUTSRQ 1
				Goto Ready3
			FlashAirKicking:
				TNT1 A 0 A_JumpIfInventory("BallistaAmmo",1,1)
				Goto FlashAirKickingUnloaded
				TNT1 A 0 A_JumpIfInventory("BallistaDemonicMode", 1, "FlashAirKickingUpgraded")
				CRBW LMNOPPPPPPONML 1
				Goto Ready3
			FlashAirKickingUpgraded:
				CRBW VWXYZZZZZZYXWV 1
				Goto Ready3
			FlashAirKickingUnloaded:
				CRBW QRSTUUUUUUTSRQ 1
				Goto Ready3
			FlashSlideKicking:
				TNT1 A 0 A_JumpIfInventory("BallistaAmmo",1,1)
				Goto FlashSlideKickingUnloaded
				TNT1 A 0 A_JumpIfInventory("BallistaDemonicMode", 1, "FlashSlideKickingUpgraded")
				CRBW LMNO 1
				CRBW P 21
				Goto Ready3
			FlashSlideKickingStop:
				TNT1 A 0 A_JumpIfInventory("BallistaAmmo",1,1)
				Goto FlashSlideKickingStopUnloaded
				TNT1 A 0 A_JumpIfInventory("BallistaDemonicMode", 1, "FlashSlideKickingStopUpgraded")
				CRBW PPPONML 1
				Goto Ready3
			FlashSlideKickingUpgraded:
				CRBW VWXY 1
				CRBW Z 21
				Goto Ready3
			FlashSlideKickingStopUpgraded:
				CRBW ZZZYXWV 1
				Goto Ready3
			FlashSlideKickingUnloaded:
				CRBW QRST 1
				CRBW U 21
				Goto Ready3
			FlashSlideKickingStopUnloaded:
				CRBW UUUTSRQ 1
				Goto Ready3
/////STATES//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
			Spawn:
				CBOW S -1
				Stop
			Select:
				TNT1 A 0 PB_HandleCrosshair(29)
				TNT1 A 0 {
					A_WeaponOffset(0,32);
					A_SetRoll(0);
					A_SetInventory("PB_LockScreenTilt",0);
					//A_ClearOverlays(10,11);	
					}
				TNT1 A 0 PB_ResetBarrelTokens	
				Goto SelectFirstPersonLegs
			SelectContinue:
				TNT1 A 0 A_SetInventory("Unloading",0)
				TNT1 A 0 A_SetInventory( "RandomHeadExploder", 1 )
			SelectAnimation:
				TNT1 A 0 A_JumpIfInventory("BallistaUnloaded", 1, "SelectAnimationUnloaded")
				TNT1 A 0 A_JumpIfInventory("BallistaDemonicMode", 1, "SelectAnimationUpgraded")
				CBOW P 1 A_PlaySoundEx("weapons/ballista/raise","Auto")
				CRBW ONML 1 A_DoPBWeaponAction
				Goto Ready3
			SelectAnimationUpgraded:
				CBOW R 1
				CRBW YXWV 1
				Goto Ready3
			SelectAnimationUnloaded:
				CBOW Q 1
				CRBW TSRQ 1
				Goto Ready3	
			Ready:
				TNT1 A 0 A_JumpIfInventory("RespectBallista", 1, "SelectAnimation")
			WeaponRespect:
				TNT1 A 0 A_GiveInventory("RespectBallista",1)
				TNT1 A 0 A_PlaySoundEx("weapons/ballista/raise","Auto")
				CBOW Q 1 A_DoPBWeaponAction
				CRBW TSRQ 1 A_DoPBWeaponAction
				CRBW A 5 A_DoPBWeaponAction
				TNT1 A 0 A_PlaySoundEx("weapons/ballista/raise","Auto")
				CBWR ABCDEF 1 {
					A_SetRoll(roll-.4, SPF_INTERPOLATE);
					A_DoPBWeaponAction;
				}
				CBWR GGGGGGGGG 1 A_DoPBWeaponAction
				TNT1 A 0 A_PlaySoundEx("Ironsights","Auto")
				CBWR HIJKKKKKKK 1 A_DoPBWeaponAction
				TNT1 A 0 A_PlaySoundEx("weapons/ballista/drawstring","Auto")
				CBWR LMNOP 1 A_DoPBWeaponAction
				TNT1 A 0 {
					A_SetInventory("BallistaDemonicMode",0);
					A_PlaySoundEx("Ironsights","Auto");
					}
				CBOR AB 1 A_DoPBWeaponAction
				TNT1 A 0 A_PlaySoundEx("weapons/ballista/boltin","Auto")
				CBOR CDE 1 A_DoPBWeaponAction
				CBOR FG 1 {
					A_SetRoll(roll-.3, SPF_INTERPOLATE);
					A_DoPBWeaponAction;
				}
				CBOR H 1 {
					A_SetRoll(roll+.3, SPF_INTERPOLATE);
					A_DoPBWeaponAction;
				}
				CBOR IJ 1 {
					A_SetRoll(roll+.4, SPF_INTERPOLATE);
					A_DoPBWeaponAction;
				}
				TNT1 A 0 A_PlaySoundEx("weapons/ballista/raise","Auto")
				CBOR KLMN 1 {
					A_SetRoll(roll+.4, SPF_INTERPOLATE);
					A_DoPBWeaponAction;
				}
				Goto Ready3
			Ready3:
				TNT1 A 0 PB_CheckBarrelIdle1()
				TNT1 A 0 PB_HandleCrosshair(29)
				TNT1 A 0 {
					A_SetRoll(0);
					A_SetInventory("PB_LockScreenTilt",0);
					}
			ReadyToFire:
				TNT1 A 0 PB_CheckBarrelIdle1()
				TNT1 A 0 A_JumpIfInventory("SwapToDemonicMode",1,"WeaponSpecial")
				TNT1 A 0 A_JumpIfInventory("BallistaAmmo",1,1)
				Goto ReadyEmpty
				TNT1 A 0 A_JumpIfInventory("BallistaDemonicMode", 1, "ReadyToFireDemonic")
				CRBW B 1 A_DoPBWeaponAction
				Goto ReadyToFire
			ReadyToFireDemonic:
				TNT1 A 0 PB_CheckBarrelIdle1()
				CRBW CDED 3 A_DoPBWeaponAction
				Goto ReadyToFire
			ReadyEmpty:
				TNT1 A 0 PB_CheckBarrelIdle1()
				TNT1 A 0 A_JumpIfInventory("PB_HighCalMag",1,"Reload")
				TNT1 A 0 { 
					if (CountInv("PB_DTech") >= 5 && CountInv("BallistaUpgraded") == 1){ return state("ReloadStartDemonic");}
					return state ("");
				}
				CRBW A 1 A_DoPBWeaponAction
				Goto ReadyEmpty
			Deselect:
				TNT1 A 0 PB_CheckBarrelIdle1()
				TNT1 A 0 A_SetCrosshair(5)
				TNT1 A 0 {
					A_WeaponOffset(0,32);
					A_SetRoll(0);
					A_SetInventory("PB_LockScreenTilt",0);
					A_TakeInventory("Reloading",1);
					A_ClearOverlays(10,11);
					A_ZoomFactor(1.0); 
				}	
				CRBW LMNO 1
				CBOW P 1
			FinishDeselect:
				TNT1 AAAAAAAAAAAAAAAAAA 0 A_Lower
				Wait
			Fire:
				TNT1 A 0 PB_CheckBarrelThrow1()
				TNT1 A 0 A_JumpIfInventory("BallistaAmmo",1,1)
				Goto ReadyEmpty
				TNT1 A 0
				TNT1 A 0 {
					A_TakeInventory("BallistaAmmo",1);
					A_ZoomFactor(0.98);
				}
				TNT1 A 0 A_JumpIfInventory("BallistaDemonicMode",1,"FireDemonic")
				TNT1 A 0 A_PlaySoundEx("weapons/ballista/firebolt","Auto")
				TNT1 A 0 A_FireCustomMissile("BallistaBolt", 0, 0, 0, -1, 0, 0)
				CRBW F 1 
				CRBW G 1 
				CRBW H 0 A_SetPitch(-1.5 + pitch)
				CRBW I 0 A_ZoomFactor(1.00)
				CRBW J 1 A_SetPitch(+1.0 + pitch)
				CRBW JJ 1 A_SetPitch(+1.0 + pitch)
				CRBW J 1 A_SetPitch(+0.5 + pitch)
				CRBW J 2 A_WeaponReady(WRF_NOFIRE| WRF_NOBOB)//Allows quick switch
				Goto ReadyEmpty
			FireDemonic:
				TNT1 A 0 A_FireCustomMissile("DemonicBolt", 0, 0, 0, -1, 0, 0)
				CRBW K 1 BRIGHT A_PlaySoundEx("weapons/ballista/firedemonic","Auto")
				CRBW G 1 BRIGHT
				CRBW H 0 A_SetPitch(-3.5 + pitch)
				CRBW I 0 A_ZoomFactor(1.00)
				CRBW J 1 A_SetPitch(+1.0 + pitch)
				CRBW JJ 1 A_SetPitch(+1.0 + pitch)
				CRBW J 1 A_SetPitch(+0.5 + pitch)
				CRBW J 2 A_WeaponReady(WRF_NOFIRE| WRF_NOBOB)//Allows quick switch
				Goto ReadyEmpty
			WeaponSpecial:
				TNT1 A 0 PB_CheckBarrelIdle1()
				TNT1 A 0 {
					A_GiveInventory("PB_LockScreenTilt",1);
					A_Takeinventory("GoWeaponSpecialAbility",1);
					A_Takeinventory("SwapToDemonicMode",1);
					A_ZoomFactor(1.0);
					A_ClearOverlays(10,11);
				}
				TNT1 A 0 A_JumpIfInventory ("BallistaUpgraded",1,2)
				TNT1 A 0 A_Print("Need Ballista Upgrade")
				Goto Ready3
				TNT1 A 0 A_JumpIfInventory ("BallistaDemonicMode",1,"WeaponSpecialDemonic")
				TNT1 A 0 A_JumpIfInventory ("PB_DTech",3,1)
				Goto Ready3
				TNT1 A 0 A_PlaySoundEx("weapons/ballista/raise","Auto")
				CBOR NMLKJI 1 A_SetRoll(roll-.4, SPF_INTERPOLATE)
				TNT1 A 0 A_PlaySoundEx("weapons/ballista/boltout","Auto")
				TNT1 A 0 A_GiveInventory("PB_HighCalMag",1)
				CBOR H 1 A_SetRoll(roll+.3, SPF_INTERPOLATE)
				CBOR GF 1 A_SetRoll(roll-.3, SPF_INTERPOLATE)
				TNT1 A 0 A_PlaySoundEx("Ironsights","Auto")
				CBOR EDCBA 1
				CBWR P 5 A_SetInventory("BallistaDemonicMode",1)
				TNT1 A 0 A_PlaySoundEx("weapons/ballista/boltinoutdemonic","Auto")
				TNT1 A 0 A_TakeInventory("PB_DTech",3)
				CBOR OPQ 1 A_SetRoll(roll+.3, SPF_INTERPOLATE)
				CBOR RST 1 A_SetRoll(roll-.3, SPF_INTERPOLATE)
				CBOR UU 1
				CBOR UU 1 A_SetRoll(roll+.4, SPF_INTERPOLATE)
				TNT1 A 0 A_PlaySoundEx("weapons/ballista/raise","Auto")
				CBOR VWXY 1 A_SetRoll(roll+.4, SPF_INTERPOLATE)
				Goto Ready3
			WeaponSpecialDemonic:
				TNT1 A 0 A_JumpIfInventory ("PB_HighCalMag",1,1)
				Goto Ready3
				TNT1 A 0 A_PlaySoundEx("weapons/ballista/raise","Auto")
				CBOR YXWVU 1 A_SetRoll(roll-.4, SPF_INTERPOLATE)
				TNT1 A 0 A_PlaySoundEx("weapons/ballista/boltinoutdemonic","Auto")
				TNT1 A 0 A_GiveInventory("PB_DTech",3)
				CBOR T 1 A_SetRoll(roll-.4, SPF_INTERPOLATE)
				CBOR SRQPO 1
				CBWR P 5 A_SetInventory("BallistaDemonicMode",0)
				CBOR AB 1
				TNT1 A 0 A_PlaySoundEx("weapons/ballista/boltin","Auto")
				TNT1 A 0 A_TakeInventory("PB_HighCalMag",1)
				CBOR CDE 1
				CBOR FG 1 A_SetRoll(roll-.3, SPF_INTERPOLATE)
				CBOR H 1 A_SetRoll(roll+.3, SPF_INTERPOLATE)
				CBOR IJ 1 A_SetRoll(roll+.4, SPF_INTERPOLATE)
				TNT1 A 0 A_PlaySoundEx("weapons/ballista/raise","Auto")
				CBOR KLMN 1 A_SetRoll(roll+.4, SPF_INTERPOLATE)
				Goto Ready3
			AltFire:
				TNT1 A 0 PB_CheckBarrelPlace1()
				TNT1 A 0 A_JumpIfInventory("BallistaAmmo",1,1)
				Goto ReadyEmpty
				TNT1 A 0
				TNT1 A 0 A_JumpIfInventory("BallistaDemonicMode",1,"AltFireDemonic")
				TNT1 A 0 A_JumpIfInventory ("PB_RocketAmmo",1,2)
				TNT1 A 0 A_Print("Need Explosive Round")
				Goto Ready3
				TNT1 A 0 {
					A_TakeInventory("BallistaAmmo",1);
					A_ZoomFactor(0.98);
				}
				TNT1 A 0 A_PlaySoundEx("weapons/ballista/firebolt","Auto")
				TNT1 A 0 A_FireCustomMissile("ExplosiveBolt", 0, 0, 0, -1, 0, 0)
				TNT1 A 0 A_TakeInventory("PB_RocketAmmo",1)
				CRBW F 1 
				CRBW G 1 
				CRBW H 0 A_SetPitch(-1.5 + pitch)
				CRBW I 0 A_ZoomFactor(1.00)
				CRBW J 1 A_SetPitch(+1.0 + pitch)
				CRBW JJ 1 A_SetPitch(+1.0 + pitch)
				CRBW J 1 A_SetPitch(+0.5 + pitch)
				CRBW J 2 A_WeaponReady(WRF_NOFIRE| WRF_NOBOB)//Allows quick switch
				Goto ReadyEmpty
			AltFireDemonic:
				TNT1 A 0 A_JumpIfInventory ("PB_Fuel",5,2)
				TNT1 A 0 A_Print("Need Fuel Ammo")
				Goto Ready3
				TNT1 A 0 {
					A_TakeInventory("BallistaAmmo",1);
					A_ZoomFactor(0.98);
				}
				TNT1 A 0 A_FireCustomMissile("RazorBlade", 0, 0, 0, -1, 0, 0)
				TNT1 A 0 A_TakeInventory("PB_Fuel",5)
				CBOW U 1 BRIGHT A_PlaySoundEx("weapons/ballista/firerazor","Auto")
				CRBW G 1 BRIGHT
				CRBW H 0 A_SetPitch(-3.5 + pitch)
				CRBW I 0 A_ZoomFactor(1.00)
				CRBW J 1 A_SetPitch(+1.0 + pitch)
				CRBW JJ 1 A_SetPitch(+1.0 + pitch)
				CRBW J 1 A_SetPitch(+0.5 + pitch)
				CRBW J 2 A_WeaponReady(WRF_NOFIRE| WRF_NOBOB)//Allows quick switch
				Goto ReadyEmpty
		   Reload:
			TNT1 A 0 PB_CheckBarrelIdle1()
			TNT1 A 0 A_JumpIfInventory ("BallistaAmmo", 1, "Ready3")
				TNT1 A 0 {
					A_SetCrosshair(5);
					A_ZoomFactor(1.0);
					A_Giveinventory("PB_LockScreenTilt",1);
					}
				TNT1 A 0 A_JumpIfInventory("BallistaDemonicMode",1,"ReloadStartDemonic")
			ReloadStart:
				TNT1 A 0 A_JumpIfInventory("PB_HighCalMag",1,1)
				Goto SwapCheckerDemonic
				TNT1 A 0 A_PlaySoundEx("weapons/ballista/raise","Auto")
				CBWR ABCDEF 1 A_SetRoll(roll-.4, SPF_INTERPOLATE)
				CBWR GGGGGGGGG 1 
				TNT1 A 0 A_PlaySoundEx("Ironsights","Auto")
				CBWR HIJKKKKKKK 1
				TNT1 A 0 A_PlaySoundEx("weapons/ballista/drawstring","Auto")
				CBWR LMNOPPPPP 1
				TNT1 A 0
				TNT1 A 0 {
					A_SetInventory("BallistaAmmo",1);
					A_SetInventory("BallistaDemonicMode",0);
					A_TakeInventory("PB_HighCalMag",1);
					A_PlaySoundEx("Ironsights","Auto");
					}
				CBOR AB 1
				TNT1 A 0 A_PlaySoundEx("weapons/ballista/boltin","Auto")
				CBOR CDE 1
				CBOR FG 1 A_SetRoll(roll-.3, SPF_INTERPOLATE)
				CBOR H 1 A_SetRoll(roll+.3, SPF_INTERPOLATE)
				CBOR IJ 1 A_SetRoll(roll+.4, SPF_INTERPOLATE)
				TNT1 A 0 A_PlaySoundEx("weapons/ballista/raise","Auto")
				CBOR KLMN 1 A_SetRoll(roll+.4, SPF_INTERPOLATE)
				Goto Ready3
			ReloadStartDemonic:
				TNT1 A 0 A_JumpIfInventory("PB_DTech",3,1)
				Goto SwapChecker
				TNT1 A 0 A_PlaySoundEx("weapons/ballista/raise","Auto")
				CBWR ABCDEF 1 A_SetRoll(roll-.4, SPF_INTERPOLATE)
				CBWR GGGGGGGGG 1 
				TNT1 A 0 A_PlaySoundEx("Ironsights","Auto")
				CBWR HIJKKKKKKKKKK 1
				TNT1 A 0 A_PlaySoundEx("weapons/ballista/drawstring","Auto")
				CBWR LMNOPPPPP 1
				TNT1 A 0
				TNT1 A 0 {
					A_SetInventory("BallistaAmmo",1);
					A_SetInventory("BallistaDemonicMode",1);
					A_TakeInventory("PB_DTech",3);
					A_PlaySoundEx("weapons/ballista/boltinoutdemonic","Auto");
					}
				CBOR OPQ 1 A_SetRoll(roll+.3, SPF_INTERPOLATE)
				CBOR RST 1 A_SetRoll(roll-.3, SPF_INTERPOLATE)
				CBOR UU 1
				CBOR UU 1 A_SetRoll(roll+.4, SPF_INTERPOLATE)
				TNT1 A 0 A_PlaySoundEx("weapons/ballista/raise","Auto")
				CBOR VWXY 1 A_SetRoll(roll+.4, SPF_INTERPOLATE)
				Goto Ready3
			SwapChecker:
				TNT1 A 0 A_JumpIfInventory("PB_HighCalMag",1,"ReloadStart")
				Goto Ready3
			SwapCheckerDemonic:
				TNT1 A 0 { 
					if (CountInv("PB_DTech") >= 5 && CountInv("BallistaUpgraded") == 1){ return state("ReloadStartDemonic");}
					return state ("");
				}
				Goto ReadyEmpty
			Steady:
				TNT1 A 1
				Goto Ready
			Unload:
				TNT1 A 0 A_TakeInventory("Unloading", 1)
				Goto Ready3	}}

/////OTHERS//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
Actor DemonicBallista: PB_UpgradeItem {
	Game Doom
	Scale 0.7
	Inventory.Pickupsound "weapons/ballista/drawstring"
	Inventory.PickupMessage "Demonic Ballista Upgrade (Slot 4)"
	+INVENTORY.ALWAYSPICKUP
	+COUNTITEM
		States {
			Spawn:
				CBOW T 10 
				LOOP
			Pickup:
				TNT1 A 0 {
					if (CountInv("BallistaUpgraded") == 0) {A_GiveInventory("SwapToDemonicMode", 1);}
				}
				TNT1 A 0 A_GiveInventory("ProSurv_Ballista", 1)
				TNT1 A 0 A_GiveInventory("BallistaUpgraded", 1)
				TNT1 A 0 A_GiveInventory("PB_DTech", 60)
				Stop	}}
				
ACTOR BallistaAmmo: PB_Ammo {
   Inventory.Amount 0
   Inventory.MaxAmount 1
   Ammo.BackpackAmount 0
   Ammo.BackpackMaxAmount 1
   +INVENTORY.IGNORESKILL
   Inventory.Icon "DEGTA0"	}

ACTOR BallistaBolt: FastProjectile {
	Radius 2
	Height 2
	Projectile
	Speed 150
    Damage 55
	Scale 1.0
    Decal Scorch
	Damagetype "Nailgun"
	Projectile
	+MISSILE
    +NOGRAVITY
    +BLOODSPLATTER 
	+THRUSPECIES
	+MTHRUSPECIES
	SeeSound ""
	DeathSound "Weapons/NailHit"
	Obituary "$OB_MPROCKET"
	Species "Marines"
		States {
			Spawn:
				CRBA A 1 A_PlaySound("Weapons/NailFlight", CHAN_BODY,1.0,True)
			Fly:
				CRBA A 2 A_SpawnItemEx("BoltTrail", 0, 0, 0, 1, 0, 0, 180, 128)
				Loop
			Death:
				TNT1 A 0 A_StopSound(CHAN_BODY)
				TNT1 A 0 A_PlaySoundEx("Weapons/NailHit","Auto")
				TNT1 A 0 A_SpawnItem("HitPuff")
				TNT1 A 0 A_Stop
				CRBA A 1 A_SpawnItemEx ("BoltPickup",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
				Stop
			XDeath:
				TNT1 A 0 A_StopSound(CHAN_BODY)
				CRBA A 0 A_PlaySound("Weapons/NailHitBleed")
				Stop	}}
				
ACTOR ExplosiveBolt: BallistaBolt {
	Damage 0
	Damagetype "Explosive"
	+DONTBOUNCEONSHOOTABLES
	+USEBOUNCESTATE
	+HITTRACER
	var int user_stickycounter;
	var int user_stuckEnemy;
		States {
			Fly:
				CRBA A 2 A_SpawnItemEx("BoltTrail", 0, 0, 0, 1, 0, 0, 180, 128)
				Loop
			Death:
			Crash:
				TNT1 A 0 {
					user_stickycounter = 0;
					A_NoGravity;
					A_ScaleVelocity(0);
				}
			Stuck:
				CRBA AAAAAAA 1  {
					//A_Fire(22);
					if(user_stuckEnemy == 1) {

						if(AAPTR_TRACER) {
							A_Warp(AAPTR_TRACER,0,0,20,0,WARPF_NOCHECKPOSITION);
						}
						else {
							A_Fall;
						}
					}
					return state("");
				}
				TNT1 A 0 {
					A_SpawnItem("GreenFlareSmall",0,0);
					A_PlaySound("BEP", 4);
					user_stickycounter++;
				}
				TNT1 A 0 A_JumpIf(user_stickycounter < 4, "Stuck")
				TNT1 A 0 A_PlaySoundEx("RA1IF1","Auto")
				CRBA AAAAA 1
				TNT1 A 0 {
					A_SpawnItem("GreenFlareSmall",0,0);
					A_PlaySound("BEP", 4);
					A_Warp(AAPTR_TRACER,0,0,20,0,WARPF_NOCHECKPOSITION);
				}
				CRBA AAAA 1
				TNT1 A 0 {
					A_SpawnItem("GreenFlareSmall",0,0);
					A_PlaySound("BEP", 4);
					A_Warp(AAPTR_TRACER,0,0,20,0,WARPF_NOCHECKPOSITION);
				}
				CRBA AA 1
				TNT1 A 0 {
					A_SpawnItem("GreenFlareSmall",0,0);
					A_PlaySound("BEP", 4);
					A_Warp(AAPTR_TRACER,0,0,20,0,WARPF_NOCHECKPOSITION);
				}
				TNT1 A 0 A_JumpIf(user_stickycounter > 4, "Detonate")
				Loop
			XDeath:
			Bounce.Creature:
				CRBA A 1 {
					A_Changeflag("THRUACTORS", 1);
					A_Changeflag("Solid", 1);
					user_stuckEnemy = 1;
					A_Stop;
				}
				CRBA A 1 {
					A_Changeflag("THRUACTORS", 0);
					A_Changeflag("Solid", 0);
				}
				Goto Stuck
			Detonate:
				TNT1 A 0 A_StopSound
				TNT1 A 0 A_PlaySound("Explosion",4)
				TNT1 A 0 A_Explode(250,150)
				TNT1 AAA 0 A_CustomMissile ("ExplosionSmoke", 22, 0, random (0, 360), 2, random (0, 360))
				TNT1 A 0 A_SpawnItemEx ("ExplosionFlareSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
				TNT1 A 0 A_SpawnItemEx ("DetectFloorCraterSmall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
				TNT1 A 0 A_SpawnItemEx ("DetectCeilCraterSmall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
				TNT1 A 0 A_SpawnItemEx ("StickyExplosion",0,0,-2,0,0,0,0,SXF_NOCHECKPOSITION,0)
				TNT1 A 0 A_SpawnItemEx ("NewRocketExploFX", 0, 0, 0)
				TNT1 AAAAA 0 A_CustomMissile ("ExplosionParticleVeryFast", 0, 0, random (0, 360), 2, random (0, 360))
				XXXX A 0 A_CustomMissile ("ExplosionQuake", 1, 0, random (0, 360), 2, random (0, 160))
				TNT1 AAAAA 0 A_CustomMissile ("MediumExplosionFlames", 0, 0, random (0, 360), 2, random (0, 360))
				TNT1 A 0 A_PlaySound("FAREXPL",3)
				EXPL AA 0 A_CustomMissile ("ExplosionSmoke", 0, 0, random (0, 360), 2, random (0, 360))  
				Stop	}}

ACTOR DemonicBolt: FastProjectile {
	Radius 5
	Height 3
	Projectile
	Speed 100
    Damage 18
	DamageType "Railgun"
	Scale 1.0
    Decal Scorch
	Projectile
	+MISSILE
	+Ripper
    +NOGRAVITY
    +EXTREMEDEATH
    +BLOODSPLATTER 
	+THRUSPECIES
	+MTHRUSPECIES
	SeeSound ""
	DeathSound "Weapons/NailHit"
	Obituary "$OB_MPROCKET"
	Species "Marines"
		States {
			Spawn:
				CRBA B 1 Bright 
				TNT1 AAAAA 0 A_CustomMissile("ObeliskTrailSpark", 0, 0, random (0, 40), 2, random (0, 160))
				TNT1 A 0 A_SpawnItem("BallistaFlare",-10,0)
				CRBA B 1 Bright
				TNT1 A 0 A_SpawnItem("BallistaFlare",-10,0)
				CRBA B 1 Bright
				TNT1 A 0 A_SpawnItem("BallistaFlare",-10,0)
				Loop
			Crash:
			Death:
				TNT1 A 0 A_SpawnItemEx ("RicoChet",0,0,-5,0,0,0,0,SXF_NOCHECKPOSITION,0)
				CRBA B 45 Bright 
				TNT1 A 0 A_PlaySoundEx("RAILIMP","Auto")
				TNT1 A 0 A_SpawnItem("ExplosionParticleSpawner")
				TNT1 A 0 A_SpawnItemEx ("SmallUnderwaterExplosion",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
				TNT1 A 0 A_SpawnItemEx ("DetectFloorCraterSmall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
				TNT1 A 0 A_SpawnItemEx ("DetectCeilCraterSmall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
				TNT1 A 0 A_CustomMissile ("BDExplosionparticlesSmall", 8, 0, random (0, 180), 2, random (40, 90))
				Stop
			XDeath:
				TNT1 A 0 A_PlaySoundEx("RAILIMP","Auto")
				TNT1 A 0 A_SpawnItem("ExplosionParticleSpawner")
				TNT1 A 0 A_SpawnItemEx ("SmallUnderwaterExplosion",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
				TNT1 A 0 A_SpawnItemEx ("DetectFloorCraterSmall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
				TNT1 A 0 A_SpawnItemEx ("DetectCeilCraterSmall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
				TNT1 A 0 A_CustomMissile ("BDExplosionparticlesSmall", 8, 0, random (0, 180), 2, random (40, 90))
				Stop	}}

Actor Razorblade {
	radius 6
	height 8
	Scale 0.75
	speed 60
	damage 20
	seesound ""
	deathsound ""
	Obituary "%o's guts were shredded by %k's flying blade."
	DamageType "Cut"
	Gravity 0
	WallBounceFactor 1
	BounceFactor 1
	Bouncecount 10
	Bouncesound "sawblade/ricochet"
	+BounceOnWalls
	+BounceOnFloors
	+BounceOnCeilings
	+BounceOnActors
	+CanBounceWater
	+UseBounceState
	PROJECTILE
	+RIPPER
	-NOGRAVITY
		states {
			Spawn:
				CRBA A 0 NoDelay
				CRBA CC 1 BRIGHT {
					A_SpawnItemEx("RazorbladeTrail",0,0,0,0,0,0,0,SXF_CLIENTSIDE);
					A_CustomMissile("ShotgunParticles", 0, 0, random (-160, -200), 2, random (0, 160));
					A_CustomMissile ("SparkX", 0, 0, random (-160, -200), 2, random (30, 170));
					A_CustomMissile ("SparkX", 0, 0, random (-160, -200), 2, random (30, 170));
					A_CustomMissile ("SparkX", 0, 0, random (-160, -200), 2, random (30, 170));
				}
				CRBA LL 1 BRIGHT {
					A_SpawnItemEx("RazorbladeTrail",0,0,0,0,0,0,0,SXF_CLIENTSIDE);
					A_CustomMissile("ShotgunParticles", 0, 0, random (-160, -200), 2, random (0, 160));
					A_CustomMissile ("SparkX", 0, 0, random (-160, -200), 2, random (30, 170));
					A_CustomMissile ("SparkX", 0, 0, random (-160, -200), 2, random (30, 170));
					A_CustomMissile ("SparkX", 0, 0, random (-160, -200), 2, random (30, 170));
				}
				TNT1 A 0 A_ChangeFlag("HitOwner",1)
				Loop
			Bounce:
				TNT1 A 0 A_SpawnItemEx ("RicoChet",0,0,-5,0,0,0,0,SXF_NOCHECKPOSITION,0)
				TNT1 AAAAAAAAAAAAAAAA 0 A_CustomMissile ("SparkX", 2, 0, random (0, 360), 2, random (30, 170))
				TNT1 AAAA 0 A_CustomMissile ("HitSpark", 2, 0, frandom(0,1)*frandom (0, 360), 2, frandom(0,1)*frandom (30, 360))
				TNT1 AAAA 0 A_CustomMissile ("HitSpark22", 2, 0, frandom(0,1)*frandom (0, 360), 2, frandom(0,1)*frandom (30, 360))
				TNT1 AAAA 0 A_CustomMissile ("HitSpark23", 2, 0, frandom(0,1)*frandom (0, 360), 2, frandom(0,1)*frandom (30, 360))
				CRBA DD 1 BRIGHT A_SpawnItemEx("RazorbladeTrail",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
				Goto Spawn
			Death:
				TNT1 A 0 {
					A_Stop;
					A_PlaySoundEx("weapons/ballista/razor","Auto");
					A_SetGravity(1.0);
				}
				TNT1 A 0 A_SpawnItemEx ("RicoChet",0,0,-5,0,0,0,0,SXF_NOCHECKPOSITION,0)
				CRBA EFGHIJK 1 BRIGHT
				CRBA L 38 BRIGHT
				TNT1 A 0 A_PlaySoundEx("RAILIMP","Auto")
				TNT1 A 0 A_SpawnItem("ExplosionParticleSpawner")
				TNT1 A 0 A_SpawnItemEx ("SmallUnderwaterExplosion",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
				TNT1 A 0 A_SpawnItemEx ("DetectFloorCraterSmall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
				TNT1 A 0 A_SpawnItemEx ("DetectCeilCraterSmall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
				TNT1 A 0 A_CustomMissile ("BDExplosionparticlesSmall", 8, 0, random (0, 180), 2, random (40, 90))
				Stop
			XDeath:
				TNT1 A 0 {
					A_Stop;
					A_PlaySoundEx("weapons/ballista/razor","Auto");
					A_SetGravity(1.0);
				}
				CRBA EFGHIJK 1 BRIGHT
				CRBA L 38 BRIGHT
				TNT1 A 0 A_PlaySoundEx("RAILIMP","Auto")
				TNT1 A 0 A_SpawnItem("ExplosionParticleSpawner")
				TNT1 A 0 A_SpawnItemEx ("SmallUnderwaterExplosion",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
				TNT1 A 0 A_SpawnItemEx ("DetectFloorCraterSmall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
				TNT1 A 0 A_SpawnItemEx ("DetectCeilCraterSmall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
				TNT1 A 0 A_CustomMissile ("BDExplosionparticlesSmall", 8, 0, random (0, 180), 2, random (40, 90))
				Stop	}}

ACTOR RazorbladeTrail {
	radius 6
	height 8
	Scale 0.95
	Alpha 0.95
	+NOCLIP
	+NOINTERACTION
	Renderstyle Translucent
		States {
			Spawn:
			CRBA CCCCCCCCCCCCCCC 1 A_FadeOut(0.15)
			Stop	}}
			
ACTOR BoltTrail : RazorBladeTrail{
	radius 2
	height 4
	Scale 1.0
		States {
			Spawn:
			CRBA AAAAAAAAAAAAAAA 1 A_FadeOut(0.15)
			Stop	}}

ACTOR BDExplosionBallistaparticlesSmall: PBExplosionparticlesSmall {
Scale 0.4
	States{
		Live:	
			SPKS ABCDEFGHI 1 BRIGHT
			Stop	}}

Actor BallistaFlare : RocketFlare {
yscale 0.2
xscale 0.2	}

ACTOR BallistaFlameTrails: FlameTrails {
Scale 0.2
Speed 2	}