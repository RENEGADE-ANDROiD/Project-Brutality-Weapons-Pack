ACTOR PB_DroppedHelmet
{
	Projectile
	+FRIENDLY
	+ROLLSPRITE
	+ROLLCENTER
	+THRUACTORS
	+BOUNCEONWALLS
	+BOUNCEONCEILINGS
	+BOUNCEONACTORS
	+BOUNCEONFLOORS
	+FORCEXYBILLBOARD
	+USEBOUNCESTATE
	-NOGRAVITY
	+CANBOUNCEWATER
	Speed 1
	Radius 19
	Height 15
	BounceType Doom
	BounceCount 4
	BounceFactor 0.5
	Gravity 1.0
	Scale 0.415
	var int user_rollleft;
	States
	{
	Spawn:
		3BON A 0 NoDelay A_SetRoll(frandom(0, 360))
		3BON A 0 A_SetUserVar(user_rollleft, random(0,1) == 0)
		3BON A 0 A_JumpIf(random(0,1) == 0, "Idle")
		3BON A 0 A_SetScale(-scalex, scaley)
	Idle:
		3BON A 0 A_ChangeFlag(THRUACTORS, false)
	Bounce:
		3BON A 0 A_StartSound("misc/abonus_PickUp", CHAN_AUTO, 0, 1, ATTN_NORM, 1.35)
		3BON AAAAAAAAAAAAAAAAAAAAAAA 2 {
			if(abs(vely) > .05 || abs(velx) > .05 || velz != 0) {
				if(user_rollleft) { A_SetRoll(roll - 5); }
				else { A_SetRoll(roll + 5); }
			}
		}
	Death:
		3BON A 0 A_SpawnItemEx("PB_DroppedHelmetItem", 0, 0, 0, velx, vely, velz, angle, SXF_TRANSFERTRANSLATION | SXF_ABSOLUTEANGLE | SXF_TRANSFERROLL | SXF_TRANSFERSCALE)
		3BON A 0
		Stop
	}
}

ACTOR PB_DroppedHelmetItem : PB_ArmorBonusDropped
{
	+ROLLSPRITE
	+ROLLCENTER
	+FORCEXYBILLBOARD
	Radius 19
	Height 15
	Scale 0.415
	States
	{
	Spawn:
		3BON AABBCCD 2
		3BON D 2 A_SpawnItem("RedFlareSmall")
		3BON EE 2 A_SpawnItem("RedFlare")
		3BON D 2 A_SpawnItem("RedFlareSmall")
		3BON DCCBB 2
		Loop
	}
}